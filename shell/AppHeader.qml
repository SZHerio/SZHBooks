import QtQuick
import QtQuick.Controls
import QtQuick.Layouts

Rectangle {
    id: root

    required property var readerController
    required property var readerWorkspace
    required property var settingsStore
    property bool darkMode: false
    property bool showingLibrary: false

    signal openRequested
    signal libraryRequested
    signal darkModeToggled(bool darkMode)

    implicitHeight: Theme.toolbarHeight
    color: Theme.chromeColor

    onShowingLibraryChanged: {
        if (root.showingLibrary) {
            readingSettings.close()
            chapterNavigation.close()
        }
    }

    Connections {
        target: root.readerWorkspace

        function onHasChaptersChanged() {
            if (!root.readerWorkspace.hasChapters) {
                chapterNavigation.close()
            }
        }
    }

    Behavior on color {
        ColorAnimation {
            duration: Theme.motionNormal
        }
    }

    RowLayout {
        anchors.fill: parent
        anchors.leftMargin: Theme.spaceLg
        anchors.rightMargin: Theme.spaceLg
        spacing: Theme.spaceSm

        Label {
            text: qsTr("Leaflit")
            color: Theme.chromeTextColor
            font.family: Theme.readingFontFamily
            font.pixelSize: Theme.titleFontSize
            font.weight: Font.DemiBold
            Layout.rightMargin: Theme.spaceSm
        }

        LeaflitIconButton {
            symbol: "\u25a6"
            toolTip: qsTr("Library")
            onChrome: true
            selected: root.showingLibrary
            onClicked: root.libraryRequested()
        }

        LeaflitButton {
            text: root.showingLibrary ? qsTr("Add book") : qsTr("Open book")
            variant: "chrome"
            onClicked: root.openRequested()
        }

        Label {
            Layout.fillWidth: true
            Layout.leftMargin: Theme.spaceSm
            Layout.rightMargin: Theme.spaceSm
            text: root.showingLibrary ? qsTr("Library") : root.readerController.title
            color: Theme.chromeMutedTextColor
            font.family: Theme.uiFontFamily
            font.pixelSize: Theme.bodyFontSize
            elide: Text.ElideRight
            horizontalAlignment: Text.AlignHCenter
            verticalAlignment: Text.AlignVCenter
        }

        Rectangle {
            visible: !root.showingLibrary && root.readerWorkspace.hasDocument
            Layout.preferredWidth: 1
            Layout.preferredHeight: 24
            Layout.leftMargin: Theme.spaceXs
            Layout.rightMargin: Theme.spaceXs
            color: Theme.chromeBorderColor
        }

        LeaflitIconButton {
            visible: !root.showingLibrary && root.readerWorkspace.hasDocument
            symbol: "-"
            toolTip: root.readerWorkspace.showingPdf
                         ? qsTr("Zoom out")
                         : qsTr("Decrease font size")
            onChrome: true
            enabled: root.readerWorkspace.canDecreaseScale
            onClicked: root.readerWorkspace.decreaseScale()
        }

        Label {
            visible: !root.showingLibrary && root.readerWorkspace.hasDocument
            text: root.readerWorkspace.scaleLabel
            color: Theme.chromeMutedTextColor
            font.family: Theme.uiFontFamily
            font.pixelSize: Theme.captionFontSize
            horizontalAlignment: Text.AlignHCenter
            verticalAlignment: Text.AlignVCenter
            Layout.preferredWidth: 58
        }

        LeaflitIconButton {
            visible: !root.showingLibrary && root.readerWorkspace.hasDocument
            symbol: "+"
            toolTip: root.readerWorkspace.showingPdf
                         ? qsTr("Zoom in")
                         : qsTr("Increase font size")
            onChrome: true
            enabled: root.readerWorkspace.canIncreaseScale
            onClicked: root.readerWorkspace.increaseScale()
        }

        LeaflitIconButton {
            visible: !root.showingLibrary && root.readerWorkspace.showingPdf
            symbol: "\u2922"
            toolTip: qsTr("Fit page to width")
            onChrome: true
            onClicked: root.readerWorkspace.fitPdfToWidth()
        }

        LeaflitIconButton {
            id: chapterButton

            visible: !root.showingLibrary && root.readerWorkspace.hasChapters
            symbol: "\u2630"
            toolTip: qsTr("Chapters")
            onChrome: true
            selected: chapterNavigation.opened
            onClicked: {
                readingSettings.close()
                chapterNavigation.opened
                    ? chapterNavigation.close()
                    : chapterNavigation.open()
            }
        }

        LeaflitIconButton {
            visible: !root.showingLibrary
            symbol: "\u2699"
            symbolPixelSize: Theme.bodyLargeFontSize
            toolTip: qsTr("Reading settings")
            onChrome: true
            selected: readingSettings.opened
            onClicked: {
                chapterNavigation.close()
                readingSettings.opened
                    ? readingSettings.close()
                    : readingSettings.open()
            }
        }

        LeaflitIconButton {
            symbol: root.darkMode ? "\u2600" : "\u263e"
            symbolPixelSize: Theme.bodyLargeFontSize
            toolTip: root.darkMode ? qsTr("Use light theme") : qsTr("Use dark theme")
            onChrome: true
            onClicked: root.darkModeToggled(!root.darkMode)
        }
    }

    ReadingSettingsPopup {
        id: readingSettings

        parent: root
        x: Math.max(Theme.spaceMd, root.width - width - Theme.spaceLg)
        y: root.height + Theme.spaceXs
        settingsStore: root.settingsStore
        textSettingsAvailable: root.readerWorkspace.showingText
    }

    ChapterNavigationPopup {
        id: chapterNavigation

        parent: root
        x: Theme.spaceMd
        y: root.height + Theme.spaceXs
        readerWorkspace: root.readerWorkspace
        onAboutToShow: {
            const buttonPosition = chapterButton.mapToItem(root, 0, 0)
            x = Math.max(Theme.spaceMd,
                         buttonPosition.x + chapterButton.width - width)
        }
    }
}
